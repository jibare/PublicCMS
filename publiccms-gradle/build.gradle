buildscript {
	repositories {
		jcenter()
	    mavenCentral()
	}
    dependencies {
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-gradle-plugin
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
    }
}
subprojects{
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: 'idea'

	ext {
	    // http://mvnrepository.com/artifact/org.springframework/spring-core
	    // http://projects.spring.io/spring-framework/
	    springVersion = "4.3.8.RELEASE"
	    // http://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
	    // http://hc.apache.org/downloads.cgi
	    httpclientVersion = "4.5.3"
	    // http://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
	    jacksonVersion = "2.8.8"
	    // http://mvnrepository.com/artifact/org.hibernate/hibernate-core
	    // http://hibernate.org/orm/downloads/
	    hibernateVersion = "5.1.6.Final"
	    // http://mvnrepository.com/artifact/org.hibernate/hibernate-search
	    // http://hibernate.org/search/downloads/
	    hibernateSearchVersion = "5.6.1.Final"
	    // http://mvnrepository.com/artifact/org.freemarker/freemarker
	    freemarkerVersion = "2.3.26-incubating"
	    // http://mvnrepository.com/artifact/org.mybatis/mybatis
	    mybatisVersion = "3.4.4"
	    // https://mvnrepository.com/artifact/org.apache.lucene/lucene-analyzers-smartcn
	    luceneVersion = "5.5.4"
	    // http://mvnrepository.com/artifact/redis.clients/jedis
	    jedisVersion = "2.9.0"
	    // http://mvnrepository.com/artifact/org.quartz-scheduler/quartz
		quartzVersion = "2.3.0"
		// http://mvnrepository.com/artifact/com.mchange/c3p0
		c3p0Version = "0.9.5.2"
	}

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	[compileJava]*.options*.encoding = "UTF-8"
	[compileTestJava]*.options*.encoding = "UTF-8"

	repositories {
		mavenLocal()
		jcenter()
	    mavenCentral()
	}
	
	eclipse {
	    classpath {
		    defaultOutputDir = file('build/classes/main')
		    file.withXml { n ->
		        n.asNode().classpathentry.findAll { it.@path.startsWith('src/test') }
		                .each { it.@output = 'build/classes/test' }
		    }
		}
	}
}
project(':publiccms-common') {
	group = 'com.publiccms'
	version = "1.0"

	dependencies {
	    compile (
	        // http://mvnrepository.com/artifact/commons-codec/commons-codec
	        "commons-codec:commons-codec:1.10",
	        // http://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	        "org.apache.commons:commons-lang3:3.5",

	        "org.freemarker:freemarker:${freemarkerVersion}",

	        "org.springframework:spring-context-support:${springVersion}",
	        "org.springframework:spring-orm:${springVersion}",
	        "org.springframework:spring-webmvc:${springVersion}",

	        "org.mybatis:mybatis:${mybatisVersion}",

	        "org.hibernate:hibernate-core:${hibernateVersion}",
	        "org.hibernate:hibernate-search-orm:${hibernateSearchVersion}",

	        "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",

	        // http://mvnrepository.com/artifact/org.apache.ant/ant
	        "org.apache.ant:ant:1.9.9",
	        // http://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
	        "javax.servlet:javax.servlet-api:3.1.0"
	    )
	    testCompile(
		    // http://mvnrepository.com/artifact/org.mybatis.generator/mybatis-generator-core
		    "org.mybatis.generator:mybatis-generator-core:1.3.5"
	    )
	}
}
project(':publiccms') {
	apply plugin: "war"
	apply plugin: "eclipse-wtp"
	/** delete this line for spring boot
	apply plugin: 'org.springframework.boot'
	delete this line for spring boot */

	group = 'org.publiccms'
	version = "V2017.0520"

	dependencies {
		compile project(':publiccms-common')
		// compile project(':publiccms-improve') //商业版插件
	    compile (
	        // "redis.clients:jedis:${jedisVersion}",  //商业版插件

	        // http://mvnrepository.com/artifact/commons-io/commons-io
	        "commons-io:commons-io:2.5",
	        // http://mvnrepository.com/artifact/commons-fileupload/commons-fileupload
	        "commons-fileupload:commons-fileupload:1.3.2",
	        // http://mvnrepository.com/artifact/commons-codec/commons-codec
	        "commons-codec:commons-codec:1.10",
	        // http://mvnrepository.com/artifact/org.apache.commons/commons-collections4
	        "org.apache.commons:commons-collections4:4.1",
	        // http://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	        "org.apache.commons:commons-lang3:3.5",

	        "org.freemarker:freemarker:${freemarkerVersion}",

	        "org.springframework:spring-context-support:${springVersion}",
	        "org.springframework:spring-orm:${springVersion}",
	        "org.springframework:spring-webmvc:${springVersion}",

	        "org.mybatis:mybatis:${mybatisVersion}",
	        // http://mvnrepository.com/artifact/org.mybatis/mybatis-spring
	        "org.mybatis:mybatis-spring:1.3.1",

	        "org.hibernate:hibernate-ehcache:${hibernateVersion}",
	        "org.hibernate:hibernate-core:${hibernateVersion}",
	        "org.hibernate:hibernate-search-orm:${hibernateSearchVersion}",
	        "org.apache.lucene:lucene-analyzers-smartcn:${luceneVersion}",
	        "org.apache.lucene:lucene-backward-codecs:${luceneVersion}",

	        "com.mchange:c3p0:${c3p0Version}",
	        // http://mvnrepository.com/artifact/mysql/mysql-connector-java
	        "mysql:mysql-connector-java:5.1.40",

	        "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
	        "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}",

	        "org.apache.httpcomponents:httpmime:${httpclientVersion}",
	        "org.apache.httpcomponents:httpclient-cache:${httpclientVersion}",

			// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
			'org.slf4j:slf4j-api:1.7.12',
	        // http://mvnrepository.com/artifact/javax.transaction/jta
	        "javax.transaction:jta:1.1",
	        // http://mvnrepository.com/artifact/javax.mail/mail
	        "javax.mail:mail:1.4.7",

	        // http://mvnrepository.com/artifact/eu.bitwalker/UserAgentUtils
	        "eu.bitwalker:UserAgentUtils:1.20",
	        // http://mvnrepository.com/artifact/net.coobird/thumbnailator
	        "net.coobird:thumbnailator:0.4.8",
	        // http://mvnrepository.com/artifact/cglib/cglib-nodep
	        "cglib:cglib-nodep:3.2.4",

	        // http://mvnrepository.com/artifact/org.apache.ant/ant
	        "org.apache.ant:ant:1.9.9",

	        "org.quartz-scheduler:quartz:${quartzVersion}"
	    )
	    providedCompile(
		    // http://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
		    "javax.servlet:javax.servlet-api:3.1.0"
	    )
	    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
	    /** delete this line for spring boot
	    compile("org.springframework.boot:spring-boot-starter-web:1.5.3.RELEASE") {
	    	exclude group:"org.springframework.boot", module: 'spring-boot-starter-logging'
	    }
	    delete this line for spring boot */
	    testCompile(
	    	// http://mvnrepository.com/artifact/junit/junit
		    "junit:junit:4.12",
		    "org.springframework:spring-test:${springVersion}"
		)
		compile fileTree(dir: 'src/main/webapp/WEB-INF/lib', include: ['*.jar'])
	}
	eclipse {
		/**
	 	delete this line for spring boot */
		file("src/main/java/config/boot/SprintBootApplication.java").renameTo(file("src/main/java/config/boot/SprintBootApplication.java.txt"))
		/** delete this line for spring boot
	 	*/
	    wtp {
	        facet {
	            facet name: "jst.java", version: "1.7"
	            facet name: "jst.web", version: "3.0"
	            facet name: "wst.jsdt.web", version: "1.0"
	        }
	    }
	}
}